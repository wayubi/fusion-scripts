//========================================================================================
// »» The Fusion Project ««
// Copyright (c) 2004, Cobax Information Systems
// http://cobax.net/
// ---------------------------------------------------------------------------------------
//
// Potions Quests - Non Player Character Locations
//
// - Milk Merchant -> prontera: 73,140
//   Transform empty bottle + 15z into Milk.
//
// - Housewife Marienu -> prt_in: 49,172
//   Mother who's saying her lil boy don't eat much.
//
// - Morrison -> prt_in: 47,173
//   The lil boy who don't eat much.
//
// - Moruhatusaya -> payon_in01: 5,48
//   Juice Maker, he transform raw items into Fruit Juices.
//
// - Pharmacist Aziyisan -> alberta_in: 16,28
//   Transform raw items into Potions.
//
//========================================================================================

prontera.gat,73,140,0 script "Milk Merchant" 86,{
	mes "[Milk Merchant]";
	mes "If you have an empty bottle, you may pay 15z and exchange it for a bottle of milk!";
	next;
	menu "Exchange all empty bottles.",-,"Set a number.",L_MENU_1,"End",L_MENU_2;
	getitemamount 713,@CHANGE_NUM;
	checkitem 713,1,L_ROOT_1,-;
	mes "[Milk Merchant]";
	mes "No, I need an empty bottle!";
	mes ".....I need one to fill it up with milk.";
	close;
	L_MENU_1:
	mes "[Milk Merchant]";
	mes "How much do you wish to exchange?";
	next;
	input @CHANGE_NUM;
	check @CHANGE_NUM == 0,L_MENU_2,-;
	L_ROOT_1:
		set @CHANGE_ZENY = @CHANGE_NUM;
		set @CHANGE_ZENY *= 15;
		checkitem 713,@CHANGE_NUM,L_ROOT_2,-;
		mes "[Milk Merchant]";
		mes "Sorry, there is not enough bottles to exchange.";
		close;
	L_ROOT_2:
		check Zeny < @CHANGE_ZENY,-,L_ROOT_3;
		mes "[Milk Merchant]";
		mes "There is enough bottles but not enough Zeny.";
		mes "You need to have $[@CHANGE_ZENY] Zeny.";
		close;
	L_ROOT_3:
		checkadditem 519,@CHANGE_NUM,L_ROOT_4,-;
		mes "[Milk Merchant]";
		mes "Sorry, if I refill those you will exceed your weight limit.";
		close;
		L_ROOT_4:
		delitem 713,@CHANGE_NUM;
		set Zeny -= @CHANGE_ZENY,0;
		additem 519,@CHANGE_NUM;
		mes "[Milk Merchant]";
		mes "Thank you.";
	L_MENU_2:
	close;
}

prt_in.gat,49,172,3 script "Housewife Marienu" 53,{
	check MORISON_MEAT == 15,-,L_START_1;
	mes "[Housewife Marienu]";
	mes "There is meat inside a fruit...";
	mes "If the fruit is tasty enough, people will try to find it and eat it.";
	next;
	set MOTHER_MARIENU = 2;
	mes "[Housewife Marienu]";
	mes "Yes! There is a certain fruit inside this village.";
	mes "Is which...";
	close;
L_START_1:
	check MOTHER_MARIENU == 1,-,L_START_2;
	mes "[Housewife Marienu]";
	mes "Morrison! Eat the fruit quickly! As for you, when you become an adult, you will be too selfish to share it.";
	mes "Are you too thick like a pig?";
	next;
	menu "Listen",-,"End",L_MENU_1;
	mes "[Housewife Marienu]";
	mes "Hey, you! Don't you think that the soul inside Morison makes stops him eating this?";
	mes "Do you think I don't know? He is not modest.";
	mes "He thinks I am foolish, don't you think?";
	next;
	mes "[Housewife Marienu]";
	mes "Come out now!";
	close;
L_MENU_1:
	mes "[Housewife Marienu]";
	mes "Morison! Stop it, unless you want me to become angry!";
	close;
L_START_2:
	check MORISON_MEAT > 0,-,L_START_3;
	set MOTHER_MARIENU = 1;
	mes "[Housewife Marienu]";
	mes "The flesh is thick and dense for its' size.";
	mes "It's this, or this!! Please come and eat more of the fruit!";
	mes "The air is smelling of it now! Come out now!";
	close;
L_START_3:
	mes "[Housewife Marienu]";
	mes "Morrison! The fruit that you don't eat quickly will rot!";
	mes "Why? The smell is not oak!";
	next;
	menu "Listen",-,"End",L_MENU_3;
	mes "[Housewife Marienu]";
	mes "Yes, this child is called Morrison, my son.";
	mes "If he does not eat the Tsuga fruit it will become bad which brings complications.";
	mes "If you do not eat this fruit it shows you have no appetite for fruit or vegetables.";
	close;
L_MENU_3:
	mes "[Housewife Marienu]";
	mes "Morrison, unless you come right now, I will become angry!";
	close;
}

prt_in.gat,47,173,3 script "Morrison" 97,{
	check MORISON_MEAT == 15,-,L_START_1;
	mes "[Morrison]";
	mes "The fruit...it is good already...people can live off eating it, but why does the air smell foul?";
	close;
L_START_1:
	check MORISON_MEAT > 9,-,L_START_2;
	mes "[Morrison]";
	mes "I'm already full. Good stuff, it is.";
	mes "If you do not give some while it is still good, your stomach will tear.";
	check MORISON_MEAT == 10,-,L_ROOT_1;
	next;
	set MORISON_MEAT += 1;
	mes "[Morrison]";
	mes "And you wil recieve.";
	mes "The fruit which tastes like candy which I have recieved is already good for me, but now you can have some too.";
	additem 529,3;
	additem 530,1;
L_ROOT_1:
	close;
L_START_2:
	mes "[Morrison]";
	mes "Fruit.";
	mes "Fruit, and you have not ate it.";
	next;
	checkitem 517,1,-,L_START_3;
	menu "Listen",-,"Show him the meat",L_MENU_2_1,"End",L_MENU_2_2;
	mes "[Morrison]";
	mes "Ah! There is a secret, but you cannot know. Now you should eat the fruit.";
	mes "As for the fruit, peel its skin one by one, but eat it, as the water gives you a funny feeling.";
	mes "It's sour also, and slippery to hold too, and does not taste good. But that is what it is meant to taste like!";
	close;
L_MENU_2_1:
	mes "[Morrison]";
	mes "The meat...";
	mes "The stomach just takes the smell from your nose...the smell...can you not just give one to me??";
	next;
	menu "Give meat",-,"Don't give",L_MENU_2_3;
	delitem 517,1;
	set MORISON_MEAT += 1;
	mes "[Morrison]";
	mes "Yes meat! Delicious!";
	mes "Thank you.";
	close;
L_MENU_2_3:
	set MORISON_MEAT = 15;
	mes "[Morrison]";
	mes "So, after all, all adults are the same!";
	close;
L_MENU_2_2:
	mes "[Morrison]";
	mes "Stop calling mother! I will not eat the fruit and vegetables!";
	close;
L_START_3:
	menu "Listen",-,"End",L_MENU_4;
	mes "[Morrison]";
	mes "Ahh! Once, and twice, it's all the same.";
	mes "As for eating the fruit, there is too much juice...";
	mes "The fruit is sour and slippery. The meat is better.";
	close;
L_MENU_4:
	mes "[Housewife Marienu]";
	mes "Well! However I may call you, you are going to eat this!";
	close;
}

payon_in01.gat,5,48,3 script "Moruhatusaya" 86,{
	check MOTHER_MARIENU == 2,-,L_START_0;
	mes "[Moruhatusaya]";
	mes "Others have come for the fruit processing. Are you one?";
	next;
	menu "Make fruit juice",-,"Listen",L_MENU_2,"End",L_MENU_3;
	mes "[Moruhatusaya]";
	mes "What kind of juice would you like to make?";
	next;
	menu "Apple juice",L_MENU_1_1,"Banana juice",L_MENU_1_2,"Carrot Juice",L_MENU_1_3,"Grape Juice",L_MENU_1_4,"End",L_MENU_1_5;
	L_MENU_1_1:
		set @BUY_ID = 512;
		set @GET_ID = 531;
		goto L_SUB_1_1;
	L_MENU_1_2:
		set @BUY_ID = 513;
		set @GET_ID = 532;
		goto L_SUB_1_1;
	L_MENU_1_3:
		set @BUY_ID = 515;
		set @GET_ID = 534;
		goto L_SUB_1_1;
	L_MENU_1_4:
		set @BUY_ID = 514;
		set @GET_ID = 533;
		L_SUB_1_1:
			getitemamount @BUY_ID,@FRUIT;
			getitemamount 713,@BOTTLE;
			set @BUY_NUM = @FRUIT;
			check @BUY_NUM > @BOTTLE,-,L_SUB_1_2;
			set @BUY_NUM = @BOTTLE;
		L_SUB_1_2:
			check @BUY_NUM == 0,L_ROOT_1_1,-;
			mes "[Merchant Morukuhasiyasi]";
			mes "How much do you want?";
			next;
			menu "Exchange all",L_SUB_1_3,"Set number",-,"End",L_MENU_1_5;
			mes "[Merchant Morukuhasiyasi]";
			mes "Select a number no more than 100 no less than 1.";
			mes "You may choose no more than $[@BUY_NUM].";
			next;
			input @BUY_NUM;
			check @BUY_NUM == 0,L_MENU_1_5,-;
		L_SUB_1_3:
			set @BUY_ZENY = @BUY_NUM;
			set @BUY_ZENY *= 3;
			checkitem @BUY_ID,@BUY_NUM,-,L_ROOT_1_1;
			checkitem 713,@BUY_NUM,-,L_ROOT_1_1;
			checkadditem @GET_ID,@BUY_NUM,-,L_ROOT_1_2;
			check Zeny >= @BUY_ZENY,-,L_ROOT_1_3;
			delitem @BUY_ID,@BUY_NUM;
			delitem 713,@BUY_NUM;
			set Zeny -= @BUY_ZENY,0;
			additem @GET_ID,@BUY_NUM;
			goto L_START_2;
L_ROOT_1_1:
	mes "[Merchant Morukuhasiyasi]";
	mes "There is not enough fruit to produce the juice.";
	close;
L_ROOT_1_2:
	mes "[Merchant Morukuhasiyasi]";
	mes "You will exceed your weight limit if you exchange for the fruit juice.";
	close;
L_ROOT_1_3:
	mes "[Merchant Morukuhasiyasi]";
	mes "There is not enough zeny to produce the juice.";
	close;
	L_MENU_1_5:
		mes "[Merchant Morukuhasiyasi]";
		mes "It goes well.";
		close;
L_START_2:
	mes "[Merchant Morukuhasiyasi]";
	mes "Yes! The juice I promised you is finished. It should taste good.";
	mes "Thank you, come again.";
	close;
	L_MENU_2:
		mes "[Merchant Morukuhasiyasi]";
		mes "Before the people on the world had many knowledge, they took fruit and wood from the earth.";
		mes "By chance, is not the fruit the biggest blessing to the world?";
		next;
		mes "[Merchant Morukuhasiyasi]";
		mes "When you have a full life, people do not have the time to eat seperate fruit.";
		mes "With that, people tried easier ways of eating it without the fuss.";
		mes "As a result, people discovered that juices could be extracted from the fruit.";
		next;
		mes "# Juice making ingredients #";
		mes "Apple juice -";
		mes "-Apple x1, Empty Bottle x1, 3 Zeny.";
		mes "";
		mes "Banana juice -";
		mes "Banana x1, Empty Bottle x1, 3 Zeny.";
		mes "";
		mes "Carrot juice";
		mes "Carrot x1, Empty bottle x1, 3 Zeny.";
		mes "";
		mes "Grape juice -";
		mes "Grape x1, Empty bottle x1, 3 Zeny.";
		close;
	L_MENU_3:
		mes "[Merchant Morukuhasiyasi]";
		mes "When a person speaks, it makes it clearer, doesn't it?";
		close;
L_START_0:
	mes "[Merchant Morukuhasiyasi]";
	mes "Is it just me and some others who share this knowledge??";
	next;
	menu "Listen",-,"End",L_MENU_4;
	mes "[Merchant Morukuhasiyasi]";
	mes "Before the people on the world had many knowledge, they took fruit and wood from the earth.";
	mes "By chance, is not the fruit the biggest blessing to the world?";
	next;
	mes "[Merchant Morukuhasiyasi]";
	mes "When you have a full life, people do not have the time to eat seperate fruit.";
	mes "With that, people tried easier ways of eating it without the fuss.";
	mes "As a result, people discovered that juices could be extracted from the fruit.";
	close;
	L_MENU_4:
		mes "[Merchant Morukuhasiyasi]";
		mes "Don't you think, when people visit me, the knowledge becomes clearer?";
		close;
}

alberta_in.gat,16,28,4 script "Pharmacist Aziyisan" 61,{
	mes "[Pharmacist Aziyisan]";
	mes "Yes? What brings you here?";
	next;
	menu "Make potion.",-,Speak.",L_MENU_2,"Ingredients.",L_MENU_3,"Nothing. (End)",L_MENU_4;
	mes "[Pharmacist Aziyisan]";
	mes "How would you like to use your materials in potions?";
	next;
	menu "Red potion.",L_MENU_1_1,"Orange potion.",L_MENU_1_2,"Yellow potion.",L_MENU_1_3,"White potion.",L_MENU_1_4,"Blue potion.",L_MENU_1_5,"Green potion.",L_MENU_1_6,"Nothing. (End)",L_MENU_1_7;
L_MENU_1_1:
	set @BUY_ID1 = 507;
	set @BUY_NEED1 = 2;
	set @BUY_ID2 = 0;
	set @BUY_NEED2 = 0;
	set @GET_ID = 501;
	set @BUY_PRICE = 3;
	goto L_SUB_1_1;
L_MENU_1_2:
	set @BUY_ID1 = 507;
	set @BUY_NEED1 = 1;
	set @BUY_ID2 = 508;
	set @BUY_NEED2 = 1;
	set @GET_ID = 502;
	set @BUY_PRICE = 5;
	goto L_SUB_1_1;
L_MENU_1_3:
	set @BUY_ID1 = 508;
	set @BUY_NEED1 = 2;
	set @BUY_ID2 = 0;
	set @BUY_NEED2 = 0;
	set @GET_ID = 503;
	set @BUY_PRICE = 10;
	goto L_SUB_1_1;
L_MENU_1_4:
	set @BUY_ID1 = 509;
	set @BUY_NEED1 = 2;
	set @BUY_ID2 = 0;
	set @BUY_NEED2 = 0;
	set @GET_ID = 504;
	set @BUY_PRICE = 20;
	goto L_SUB_1_1;
L_MENU_1_5:
	set @BUY_ID1 = 510;
	set @BUY_NEED1 = 2;
	set @BUY_ID2 = 0;
	set @BUY_NEED2 = 0;
	set @GET_ID = 505;
	set @BUY_PRICE = 30;
	goto L_SUB_1_1;
L_MENU_1_6:
	set @BUY_ID1 = 511;
	set @BUY_NEED1 = 2;
	set @BUY_ID2 = 0;
	set @BUY_NEED2 = 0;
	set @GET_ID = 506;
	set @BUY_PRICE = 3;
L_SUB_1_1:
	getitemamount @BUY_ID1,@POTION1;
	set @POTION1 /= @BUY_NEED1;
	check @BUY_ID2 > 0,-,L_SUB_1_2;
	getitemamount @BUY_ID2,@POTION2;
	set @POTION2 /= @BUY_NEED2;
L_SUB_1_2:
	getitemamount 713,@BOTTLE;
	set @BUY_NUM = @POTION1;
	check @BUY_ID2 > 0,-,L_SUB_1_3;
	check @BUY_NUM > @POTION2,-,L_SUB_1_3;
	set @BUY_NUM = @POTION2;
	check @BUY_NUM > @BOTTLE,-,L_SUB_1_3;
	set @BUY_NUM = @BOTTLE;
L_SUB_1_3:
	check @BUY_NUM == 0,L_ROOT_1_1,-;
	mes "[Pharmacist Aziyisan]";
	mes "How much do you want?";
	next;
	menu "Exchange all.",L_SUB_1_4,"Select an amount.",-,"Nothing. (End)",L_MENU_1_7;
	mes "[Pharmacist Aziyisab]";
	mes "Choose a number no more than 100 no less than 1.";
	mes "You may not exchange more than $[@BUY_NUM].";
	next;
	input @BUY_NUM;
	check @BUY_NUM == 0,L_MENU_1_7,-;
L_SUB_1_4:
	set @BUY_ZENY = @BUY_NUM;
	set @BUY_ZENY *= @BUY_PRICE;
	set @BUY_NUM1 = @BUY_NUM;
	set @BUY_NUM1 *= @BUY_NEED1;
	checkitem @BUY_ID1,@BUY_NUM1,-,L_ROOT_1_1;
	check @BUY_ID2 > 0,-,L_SUB_1_5;
	set @BUY_NUM2 = @BUY_NUM;
	set @BUY_NUM2 *= @BUY_NEED2;
	checkitem @BUY_ID2,@BUY_NUM2,-,L_ROOT_1_1;
L_SUB_1_5:
	checkitem 713,@BUY_NUM,-,L_ROOT_1_1;
	checkadditem @GET_ID,@BUY_NUM,-,L_ROOT_1_3;
	check Zeny >= @BUY_ZENY,-,L_ROOT_1_2;
	delitem @BUY_ID1,@BUY_NUM1;
	check @BUY_ID2 > 0,-,L_SUB_1_6;
	delitem @BUY_ID2,@BUY_NUM2;
L_SUB_1_6:
	delitem 713,@BUY_NUM;
	set Zeny -= @BUY_ZENY,0;
	additem @GET_ID,@BUY_NUM;
	goto L_START_2;
L_ROOT_1_1:
	mes "[Pharmacist Aziyisan]";
	mes "Do you know its' a bad habit to leave materials behind?";
	mes "Return with the proper materials!";
	close;
L_ROOT_1_2:
	mes "[Pharmacist Aziyisan]";
	mes "Do you know its' a bad habit to leave your Zeny behind?";
	mes "Return with the proper materials!";
	close;
L_ROOT_1_3:
	mes "[Pharmacist Aziyisan]";
	mes "You will exceed the weight limit.";
	mes "Return with a lower carry weight!";
	close;
L_START_2:
	mes "[Pharmacist Aziyisan]";
	mes "Pay attention!";
	mes "Using too much medicine is bad for the body!";
	close;
L_MENU_1_7:
	mes "[Pharmacist Aziyisan]";
	mes "What? This makes no sense.";
	close;
L_MENU_2:
	mes "[Pharmacist Aziyisan]";
	mes "Medicine greatly expands the healing speed of the body.";
	mes "When you grow older, you will experience that.";
	mes "In anycase, make sure you use the right amount of medicine.";
	close;
L_MENU_3:
	mes "[Pharmacist Aziyisan]";
	mes "You should always pay attention to the label on any medicine.";
	next;
	mes "[Pharmacist Aziyisan]";
	mes "Making herbs into a potion makes the herbs much more effective.";
	next;
	mes "[Pharmacist Aziyisan]";
	mes "But making it is not as easy as it seems. I have tried many times, and now I have mastered the skill.";
	next;
	mes "[Pharmacist Aziyisan]";
	mes "Red potion - ^0098E5 Red Herb x2, Empty Bottle x1, 3 Zeny.^000000";
	mes "Orange Potion - ^0098E5 Red herb x1, Yellow Herb x1, Empty Bottle x1, 5 Zeny.^000000";
	mes "Yellow Potion - ^0098E5 Yellow herb x2, Empty Bottle x1, 10 Zeny.^000000";
	next;
	mes "[Pharmacist Aziyisan]";
	mes "White Potion - ^0098E5 White Herb x2, Empty Bottle x1, 20 Zeny.^000000";
	mes "Blue Potion - ^0098E5 Blue Herb x2, Empty Bottle x1, 30 Zeny.^000000";
	mes "Green Potion - ^0098E5 Green Herb x2, Empty Bottle x1, 3 Zeny.^000000";
	close;
L_MENU_4:
	mes "[Pharmacist Aziyisan]";
	mes "What? What I am speaking is clear and loud!";
	close;
}
