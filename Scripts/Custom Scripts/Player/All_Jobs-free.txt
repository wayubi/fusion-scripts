// =======================================================================================
// »» The Fusion Project ««
// Copyright (c) 2004, Cobax Information Systems
// http://fusion.cobax.net/
// ---------------------------------------------------------------------------------------
//
// - Minister of Free Jobs -> prontera 164 183
// 	Job Changer for all jobs, including 2-3(Advanced 2nd Job) and Super Novice.
//
// =======================================================================================

prontera.gat,164,183,4 script "Minister of Free Jobs" 110,{
	mes "[Minister of Free Jobs]";
	mes "Greetings $charaname!";
	next;
	mes "[Minister of Free Jobs]";
	mes "I guess you want to change your class?";
	next;
	menu "Yes",-,"No",L_NEVERMIND;
		mes "[Minister of Free Jobs]";
		mes "Let me check your possibilities...";
		next;
		goto L_CHECKJOB;
	L_CHECKJOB:
		check Job != 22,-,L_NOJOBS;
		check Job != 23,-,L_ERRSUPERNOV;
		check SkillPoint = 0,-,L_SKILLPOINT;
		check Job > 0,-,L_JOBNOVICE;
		check Job > 6,-,L_1STCLASS;
		check Job > 21,-,L_JOBHIGHNOV;
		check Job > 24,-,L_HIGH1st;
		check Job > 30,L_NOMOREJOBS,L_HIGH2ND;
	L_JOBNOVICE:
		check JobLevel = 10,-,L_REQJLEVEL;
		mes "[Minister of Free Jobs]";
		mes "Ah, young one, this is a list of the classes you can improve.";
		mes " ";
		next;
		menu "Swordman",L_CHGSWORD,"Mage",L_CHGMAGE,"Archer",L_CHGARCHER,"Acolyte",L_CHGACO,"Merchant",L_CHGMERC,"Thief",L_CHGTHIEF,"Super Novice (Req Base Level 60)",L_SUPERNOVICE;
		close;
		L_CHGSWORD:
			jobchange 1;
			set HIGHNOVFLAG = 1;
			goto L_SETJOBLEVEL;
		L_CHGMAGE:
			jobchange 2;
			set HIGHNOVFLAG = 2;
			goto L_SETJOBLEVEL;
		L_CHGARCHER:
			jobchange 3;
			set HIGHNOVFLAG = 3;
			goto L_SETJOBLEVEL;
		L_CHGACO:
			jobchange 4;
			set HIGHNOVFLAG = 4;
			goto L_SETJOBLEVEL;
		L_CHGMERC:
			jobchange 5;
			set HIGHNOVFLAG = 5;
			goto L_SETJOBLEVEL;
		L_CHGTHIEF:
			jobchange 6;
			set HIGHNOVFLAG = 6;
			goto L_SETJOBLEVEL;
		L_SUPERNOVICE:
			check BaseLevel = 60,-,L_REQBLEVEL;
			mes "[Minister of Free Jobs]";
			mes "Before I change you to Super Novice, I will tell you that this choice is permanent, you can't change later to other classes.";
			next;
			mes "[Minister of Free Jobs]";
			mes "So, are you really sure that do you want to change?";
			next;
			menu "Yes",-,"No",L_NEVERMIND;
				jobchange 23;
				goto L_SETJOBLEVEL;
	L_1STCLASS:
		check JobLevel > 39,-,L_REQJLEVEL;
		mes "[Minister of Free Jobs]";
		mes "Ah, I can see you've got more experience. I'll give you right away your options...";
		next;
		goto L_CHECK1ST;
		L_CHECK1ST:
			check Job > 1,-,L_JOBSWORD;
			check Job > 2,-,L_JOBMAGE;
			check Job > 3,-,L_JOBARCHER;
			check Job > 4,-,L_JOBACO;
			check Job > 5,L_JOBTHIEF,L_JOBMERC;
		L_JOBSWORD:
			menu "Knight",L_CHGKNIGHT,"Crusader",L_CHGCRUSADER,"Nevermind",L_NEVERMIND;
				L_CHGKNIGHT:
					jobchange 7;
					goto L_SETJOBLEVEL;
				L_CHGCRUSADER:
					jobchange 14;
					goto L_SETJOBLEVEL;
		L_JOBMAGE:
			menu "Wizard",L_CHGWIZARD,"Sage",L_CHGSAGE,"Nevermind",L_NEVERMIND;
				L_CHGWIZARD:
					jobchange 9;
					goto L_SETJOBLEVEL;
				L_CHGSAGE:
					jobchange 16;
					goto L_SETJOBLEVEL;
		L_JOBARCHER:
			check Gender = 0,-,L_1STMALE;
			menu "Dancer",L_CHGDANCER,"Hunter",L_CHGHUNTER,"Nevermind",L_NEVERMIND;
				L_CHGDANCER:
					jobchange 20;
					goto L_SETJOBLEVEL;
			L_1STMALE:
				menu "Bard",L_CHGBARD,"Hunter",L_CHGHUNTER,"Nevermind",L_NEVERMIND;
					L_CHGBARD:
						jobchange 19;
						goto L_SETJOBLEVEL;
			L_CHGHUNTER:
				jobchange 11;
				goto L_SETJOBLEVEL;
		L_JOBACO:
			menu "Priest",L_CHGPRIEST,"Monk",L_CHGMONK,"Nevermind",L_NEVERMIND;
				L_CHGPRIEST:
					jobchange 8;
					goto L_SETJOBLEVEL;
				L_CHGMONK:
					jobchange 15;
					goto L_SETJOBLEVEL;
		L_JOBMERC:
			menu "Blacksmith",L_CHGSMITH,"Alchemist",L_CHGALCHEM,"Nevermind",L_NEVERMIND;
				L_CHGSMITH:
					jobchange 10;
					goto L_SETJOBLEVEL;
				L_CHGALCHEM:
					jobchange 18;
					goto L_SETJOBLEVEL;
		L_JOBTHIEF:
			menu "Assassin",L_CHGASSA,"Rogue",L_CHGROGUE,"Nevermind",L_NEVERMIND;
				L_CHGASSA:
					jobchange 12;
					goto L_SETJOBLEVEL;
				L_CHGROGUE:
					jobchange 17;
					goto L_SETJOBLEVEL;
	L_JOBHIGHNOV:
		check BaseLevel > 98,-,L_REQBLEVEL;
		check JobLevel > 49,-,L_REQJLEVEL;
		mes "[Minister of Free Jobs]";
		mes "Ah! So you want to aspire for the 2-3 advanced classes? First you'll need to turn to novice again...";
		next;
		mes "[Minister of Free Jobs]";
		mes "Is that ok with you?";
		next;
		menu "Yes",-,"No",L_NEVERMIND;
			mes "[Minister of Free Jobs]";
			mes "Ok, you asked for it...";
			next;
			ccolor 0;
			jobchange 24;
			set baselevel = 1,0;
			set JobLevel = 1,0;
			resetstat;
			set statuspoint = 100;
			set skillpoints = 0;
			setskilllevel 0x1,0;
			close;
	L_HIGH1st:
		check Job = 24,-,L_NOJOBS;
		check JobLevel = 10,-,L_REQJLEVEL;
		mes "[Minister of Free Jobs]";
		mes "Ah! I can see you have a lot of free time to make it this far... ^_^";
		mes "I'll change you right away if that is ok with you.";
		next;
		menu "Yes",-,"No",L_NEVERMIND;
			check HIGHNOVFLAG > 1,-,L_HIGHSWORD;
			check HIGHNOVFLAG > 2,-,L_HIGHMAGE;
			check HIGHNOVFLAG > 3,-,L_HIGHARCH;
			check HIGHNOVFLAG > 4,-,L_HIGHACO;
			check HIGHNOVFLAG > 5,L_HIGHTHIEF,L_HIGHMERC;
		L_HIGHSWORD:
			jobchange 25;
			goto L_SETJOBLEVEL;
		L_HIGHMAGE:
			jobchange 26;
			goto L_SETJOBLEVEL;
		L_HIGHARCH:
			jobchange 27;
			goto L_SETJOBLEVEL;
		L_HIGHACO:
			jobchange 28;
			goto L_SETJOBLEVEL;
		L_HIGHMERC:
			jobchange 29;
			goto L_SETJOBLEVEL;
		L_HIGHTHIEF:
			jobchange 30;
			goto L_SETJOBLEVEL;
	L_HIGH2ND:
		check JobLevel > 39,-,L_REQJLEVEL;
		mes "[Minister of Free Jobs]";
		mes "What? You still here? I guess you're a real RO Fanatic...";
		mes "Here is the list of posibilities...";
		next;
		check Job > 25,-,L_ADVSWORD;
		check Job > 26,-,L_ADVMAGE;
		check Job > 27,-,L_ADVARCHER;
		check Job > 28,-,L_ADVACO;
		check Job > 29,L_ADVTHIEF,L_ADVMERC;
		L_ADVSWORD:
			menu "Lord Knight",L_LORDKNIGHT,"Paladin",L_PALADIN,"Nevermind",L_NEVERMIND;
			L_LORDKNIGHT:
				jobchange 31;
				goto L_SETJOBLEVEL;
			L_PALADIN:
				jobchange 38;
				goto L_SETJOBLEVEL;
		L_ADVMAGE:
			menu "High Wizard",L_HIGHWIZARD,"Professor",L_PROFESSOR,"Nevermind",L_NEVERMIND;
			L_HIGHWIZARD:
				jobchange 33;
				goto L_SETJOBLEVEL;
			L_PROFESSOR:
				jobchange 40;
				goto L_SETJOBLEVEL;
		L_ADVARCHER:
			check Gender = 0,-,L_HIGHMALE;
			menu "Gypsy",L_GYPSY,"Sniper",L_SNIPER,"Nevermind",L_NEVERMIND;
			L_GYPSY:
				jobchange 44;
				goto L_SETJOBLEVEL;
			L_HIGHMALE:
				menu "Clown",L_CLOWN,"Sniper",L_SNIPER;
				close;
				L_CLOWN:
					jobchange 43;
					goto L_SETJOBLEVEL;
			L_SNIPER:
				jobchange 35;
				goto L_SETJOBLEVEL;
		L_ADVACO:
			menu "High Priest",L_HIGHPRIEST,"Champion",L_CHAMPION,"Nevermind",L_NEVERMIND;
			L_HIGHPRIEST:
				jobchange 32;
				goto L_SETJOBLEVEL;
			L_CHAMPION:
				jobchange 39;
				goto L_SETJOBLEVEL;
		L_ADVMERC:
			menu "Whitesmith",L_WHITESMITH,"Creator",L_CREATOR,"Nevermind",L_NEVERMIND;
			L_WHITESMITH:
				jobchange 34;
				goto L_SETJOBLEVEL;
			L_CREATOR:
				jobchange 42;
				goto L_SETJOBLEVEL;
		L_ADVTHIEF:
			menu "Assassin Cross",L_ASSCROSS,"Stalker",L_STALKER,"Nevermind",L_NEVERMIND;
			L_ASSCROSS:
				jobchange 36;
				goto L_SETJOBLEVEL;
			L_STALKER:
				jobchange 41;
				goto L_SETJOBLEVEL;
	L_SETJOBLEVEL:
		set JobLevel = 1,0;
		close;
	L_SKILLPOINT:
		mes "[Minister of Free Jobs]";
		mes "You need to spend all your skill points before changing your class.";
		close;
	L_NOJOBS:
		check Job = 22,-,L_ERRSUPERNOV;
		mes "[Minister of Free Jobs]";
		mes "You need to change clothes, before you can change your job.";
		close;
	L_ERRSUPERNOV:
		mes "[Minister of Free Jobs]";
		mes "The choice of Super Novice is permanent! You can't change jobs now!";
		close;
	L_NOMOREJOBS:
		mes "[Minister of Free Jobs]";
		mes "There arent any more Jobs for you to change into.. make another character or something! :P";
		close;
	L_REQBLEVEL:
		mes "[Minister of Free Jobs]";
		mes "You need more base levels, in order to change to the class you selected.";
		close;
	L_REQJLEVEL:
		mes "[Minister of Free Jobs]";
		mes "You need more job levels, in order to change to the class you selected.";
		close;
	L_NEVERMIND:
		mes "[Minister of Free Jobs]";
		mes "Well, come back, if you ever change your mind. ^,^";
		close;
}
